description = 'Statically typed i18n for Java'

configure(allprojects) { project ->
  group = 'de.bripkens'
  version = '0.2.0'

  apply plugin: 'java'
  apply plugin: 'groovy'
  apply plugin: 'eclipse'

  compileJava {
    sourceCompatibility = 1.6
    targetCompatibility = 1.6
  }

  compileGroovy {
    sourceCompatibility = 1.6
    targetCompatibility = 1.6
  }

  repositories {
    mavenCentral()
  }

  dependencies {
    testCompile 'org.codehaus.groovy:groovy-all:2.2.2'
    testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
  }
}


configure(subprojects) { subproject ->

  jar {
    manifest.attributes["Created-By"] = "${System.getProperty("java.version")} (${System.getProperty("java.specification.vendor")})"
    manifest.attributes["Implementation-Title"] = project.name
    manifest.attributes["Implementation-Version"] = project.version
  }

  task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
  }

  task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
  }

  artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
  }

  apply plugin: 'maven'
  apply plugin: 'signing'

  signing {
    sign configurations.archives
  }

  uploadArchives {
    repositories {
      mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
          authentication(userName: sonatypeUsername, password: sonatypePassword)
        }

        pom.project {
          name project.name
          packaging 'jar'
          description project.description
          url 'https://github.com/bripkens/static-i18n'

          scm {
            url 'scm:git@github.com:bripkens/static-i18n.git'
            connection 'scm:git@github.com:bripkens/static-i18n.git'
            developerConnection 'scm:git@github.com:bripkens/static-i18n.git'
          }

          licenses {
            license {
              name 'The Apache Software License, Version 2.0'
              url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
              distribution 'repo'
            }
          }

          developers {
            developer {
              id 'bripkens'
              name 'Ben Ripkens'
              email 'bripkens.dev@gmail.com'
            }
          }
        }
      }
    }
  }
}

configure(rootProject) {
  configurations.archives.artifacts.clear()
}
